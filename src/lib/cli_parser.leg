# vi:set ts=4 sw=4 expandtab:
#
# Copyright 2016, Chris Leishman (http://github.com/cleishm)
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

__entry_points = directive | argument

directive = - (command | statement)

command = < command-body > line-end            { capture(yy, yypos, true); }
        | < command-body > EOF                 { capture(yy, yypos, false); }
command-body = ':' (quoted | !line-end .)*
line-end = (HWS | block-comment)* (line-comment | EOL)

statement = < statement-body > - ';'           { capture(yy, yypos, true); }
          | < statement-body > - EOF           { capture(yy, yypos, false); }
statement-body = (comment | quoted | ESC | !(- ';') .)*

comment = line-comment | block-comment
line-comment = '//' (!EOL .)* EOL
block-comment = '/*' (!'*/' .)* '*/'

quoted = single-quoted | double-quoted
single-quoted = "'" (ESC | !"'" .)* "'"
double-quoted = '"' (ESC | !'"' .)* '"'

argument = WS* argument-body WS*
         | WS+ <>                              { capture(yy, yypos, false); }
argument-body = "'" < (ESC | !"'" .)* > "'"    { capture(yy, yypos, true); }
              | '"' < (ESC | !'"' .)* > '"'    { capture(yy, yypos, true); }
              | < (ESC | !(WS | EOF | "'" | '"') .)+ >
                                               { capture(yy, yypos, true); }

- = (WS | comment)*
WS = HWS | EOL
HWS = [ \t]
ESC = '\\' .
EOL = '\n' | '\r\n' | '\r'
EOF = !.
